# Maintainer: Andrea Corsini <andrea.corsini@outlook.com>
pkgname=surf-git
pkgver=2.0.r73.d068a38
pkgrel=1
pkgdesc="Simple web browser based on WebKit2/GTK+"
arch=('i686' 'x86_64')
url="https://surf.suckless.org"
license=('MIT/X')
depends=('webkit2gtk' 'gcr' 'xorg-xprop')
makedepends=('git')
optdepends=('dmenu: url bar and search'
            'tabbed: tab browsing'
            'ca-certificates: SSL verification'
            'st: default terminal for the download handler'
            'curl: default download handler'
            'mpv: default video player')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://git.suckless.org/surf"
        'surf-bookmarks-20170722-723ff26.diff'
        'surf-custom-path-keyb-20190719-d068a38.diff'
        'surf-custom-websearch-20190719-d068a38.diff')
md5sums=('SKIP'
         'b4b7f920a7782d8ed6b6dbb3052409b1'
         'f44e508c9305d68e0553f810f0f3c632'
         'b88c38ebc29033677e01d702cacf5cb5')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "%s" "$(git describe --tag | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
  cd "$srcdir/${pkgname%-git}"
  for file in "${source[@]}"; do
    if [[ "$file" == *.diff || "$file" == *.patch ]]; then
      # add all patches present in source array
      patch -Np1 < "$srcdir/$(basename ${file})"
    fi
  done
}

build() {
	cd "$srcdir/${pkgname%-git}"
	make PREFIX=/usr
} 

package() {
	cd "$srcdir/${pkgname%-git}"
	make DESTDIR="$pkgdir/" PREFIX=/usr install
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/${pkgname%-git}/LICENSE"
}
