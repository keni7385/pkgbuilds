# Maintainer: Andrea Corsini <andrea.corsini@outlook.com>
pkgname=slock-git
pkgver=1.4.r9.ea42f00
pkgrel=1
pkgdesc="A simple X display locker"
arch=('i686' 'x86_64')
url="https://tools.suckless.org/slock"
license=('MIT/X')
depends=('libxrandr')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+ssh://git@github.com/keni7385/slock.git")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "%s" "$(git describe --tag | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	make PREFIX=/usr X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
} 

package() {
	cd "$srcdir/${pkgname%-git}"
	make DESTDIR="$pkgdir/" PREFIX=/usr install
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/${pkgname%-git}/LICENSE"
}
